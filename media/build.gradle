apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'//添加bintray插件
apply plugin: 'com.github.dcendents.android-maven'

//版本
def libVersion = '1.3.0'
//项目主页
def siteUrl = 'https://github.com/hemdenry/MediaSelector'
//项目的版本控制地址
def gitUrl = 'https://github.com/hemdenry/MediaSelector.git'

group = 'com.hemdenry.media'
version = libVersion

android {

    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 4
        versionName libVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.github.yalantis:ucrop:2.2.4'
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']

    pkg {
        repo = 'MediaSelector'
        name = 'com.hemdenry.media'
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = siteUrl

        version {
            name = libVersion
            desc = "Android media selector."
            vcsTag = libVersion
            released = new Date()
        }
    }

    javadoc {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Android media selector'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'hemdenry'
                        name 'hemdenry'
                        email 'hemdenry@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}